apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

configurations {
    ktlint
}

android {
    signingConfigs {
        integrationConfig {
            keyAlias 'integration-key'
            keyPassword 'password'
            storeFile file("${rootDir}/opensportmanagement-android.jks")
            storePassword 'password'
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.zcorp.opensportmanagement"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildToolsVersion "28.0.1"
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:support-compat:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation 'com.google.android.gms:play-services-plus:15.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:support-vector-drawable:$supportLibraryVersion"

    //For API < 21
    implementation 'com.android.support:multidex:1.0.3'

    //  Architecture
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.16'

    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation 'android.arch.lifecycle:reactivestreams:1.1.1'
    kapt "android.arch.lifecycle:compiler:1.1.1"

    implementation "android.arch.persistence.room:runtime:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"
    implementation "android.arch.persistence.room:rxjava2:1.1.1"

    implementation "android.arch.paging:runtime:$paging_library_version"
    implementation "android.arch.paging:rxjava2:$paging_library_version"

    // UI
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.anthonymandra:ToggleButtons:2.0.0'
    implementation 'com.afollestad.material-dialogs:core:2.0.0-alpha05'
    implementation 'com.afollestad.material-dialogs:input:2.0.0-alpha05'
    implementation 'io.github.luizgrp.sectionedrecyclerviewadapter:sectionedrecyclerviewadapter:1.2.0'

    // Koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"
    androidTestImplementation "org.koin:koin-test:$koin_version"

    //    JSR-310 implementation
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'
    //    HTTP requests
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:3.9.0"
    implementation "com.github.franmontiel:PersistentCookieJar:v1.0.1"

    // JSON serialization/deserialization
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.0'
    implementation "com.github.joschi.jackson:jackson-datatype-threetenbp:2.8.4", {
        exclude group: 'org.threeten'
    }
    implementation 'com.squareup.retrofit2:converter-jackson:2.3.0'

    // Lint
    ktlint "com.github.shyiko:ktlint:0.24.0"

    // WebSocket
    implementation 'com.github.NaikSoftware:StompProtocolAndroid:1.4.4'
    //  Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.0-alpha-3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    testImplementation 'com.willowtreeapps.assertk:assertk:0.10'
    // Test helpers for LiveData
    testImplementation "android.arch.core:core-testing:1.1.1"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
